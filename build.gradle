plugins {
    id 'java'
    id 'application'
}

group 'com.kohyunwoo.kisorder'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.json:json:20240303'
    implementation 'org.apache.pdfbox:pdfbox:2.0.28'
    implementation 'org.apache.pdfbox:pdfbox-tools:2.0.28'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

application {
    mainClass = 'SarifToPdf' // SarifToPdf 클래스를 메인 클래스로 설정
}

jar {
    manifest {
        attributes 'Main-Class': 'SarifToPdf' // SarifToPdf 클래스를 메인 클래스로 설정
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    reports {
        junitXml.required = true
        html.required = true
    }
}

tasks.register('createPdfReportDir') {
    doLast {
        mkdir "${buildDir}/reports/pdf"
    }
}

build.dependsOn createPdfReportDir