plugins {
    id 'java'
    id 'application'
}

group 'com.kohyunwoo.kisorder'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.json:json:20240303'
    implementation 'org.apache.pdfbox:pdfbox:2.0.28'
    implementation 'org.apache.pdfbox:pdfbox-tools:2.0.28'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

application {
    mainClass = 'SarifToPdf'
}

jar {
    manifest {
        attributes 'Main-Class': 'SarifToPdf'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Include the fonts folder in the JAR file
    from('src/resources/fonts') {
        include '*.TTF'
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'CodeTest'] // CodeTest 폴더 추가
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    reports {
        junitXml.required = true
        html.required = true
    }
}

tasks.register('createPdfReportDir') {
    doLast {
        mkdir "${buildDir}/reports/pdf"
    }
}

build.dependsOn createPdfReportDir